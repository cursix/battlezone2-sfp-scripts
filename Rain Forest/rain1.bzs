// Mission 1 for "The Rain Forest"
// Written by {SFP} Sonic
// Copyrighted The Starfleet Platoon

[objects]
Player
Recycler
Enemy_Recycler
Enemy_Factory
Enemy_Armory
Engineer1
Attacker
NewUnit

[positions]
Enemy_Recy_Spawn,0,0,0

[variables]
None,0

[text]
Objective1,
"Stand By for Orders"

Objective2,
"Destroy the ISDF Attack\nForce"

Objective3,
"Your Recycler Must Survive"

[routine,Main,1,true]
GetPlayer,Player
Replace,Player,"fvtank",false
SetScrap,1,40
SetScrap,5,60
Display,Objective1,white
Audio,"chatter6.wav"
Wait,2
Audio,"rain1a.wav"
Wait,8
Audio,"chatter8.wav"
Wait,3
Audio,"rain1b.wav"
Clear
Display,Objective2,white
Display,Objective3,white
SetPlan,"rain1main.aip",5
CHECK:
IsAround,Enemy_Recycler
IfEQ,true,CHECK
GetByTS,Enemy_Factory,5,2
CHECK_FACTORY:
IsAround,Enemy_Factory
IfEQ,true,CHECK_FACTORY
Clear
Display,Objective2,green
Audio,"rain1c.wav"
Succeed,10,"rain1w.txt"

[routine,CheckRecycler,1,true]
GetByLabel,Recycler,"unnamed_fvrecy"
CHECK:
IsAround,Recycler
IfEQ,true,CHECK
Clear
Display,Objective3,red
Fail,10,"rain1_lose.txt"

[routine,SetUpEngineer,1,true]
GetByTS,Enemy_Recycler,5,1
GetPos,Enemy_Recycler,Enemy_Recy_Spawn
Ally,5,6
Wait,660
SetPlan,"rain1eng1.aip",6
BUILD_CONS:
Create,Engineer1,"iveng",6,Enemy_Recy_Spawn
CHECK:
IsAround,Engineer1
IfEQ,true,CHECK
Wait,300
JumpTo,BUILD_CONS
//START:
//Build,Engineer1,"ibraven",1
//DropOff,Engineer1,"raven1",1
//Build,Engineer1,"ibraven",1
//DropOff,Engineer1,"raven2",1
//Build,Engineer1,"ibrbun",1
//DropOff,Engineer1,"rcomm1",1
//Build,Engineer1,"ibraven",1
//DropOff,Engineer1,"raven3",1
//Build,Engineer1,"ibraven",1
//DropOff,Engineer1,"raven4",1
//JumpTo,CHECK

[routine,Suprise,1,true]
Wait,600
Createp,Attacker,"ivltank",5,"attack1"
Attack,Attacker,Recycler,1
Createp,Attacker,"ivltank",5,"attack1"
Attack,Attacker,Recycler,1
Createp,Attacker,"ivltank",5,"attack1"
Attack,Attacker,Recycler,1
Createp,Attacker,"ivltank",5,"attack1"
Attack,Attacker,Recycler,1

[routine,NewObjectRoutine,1,true]
OnNewObject,100,NewObjectRoutine,NewUnit
BEGIN:
WAITING:
GetByTS,Enemy_Armory,5,3
IsAround,Enemy_Armory
IfEQ,false,WAITING
RunSpeed,NewObjectRoutine,0,true
SetSkill,NewUnit,3
IsODF,NewUnit,"ivscout"
IfEQ,false,NOT_SCOUT
GiveWeapon,NewUnit,"gchain_c"
GiveWeapon,NewUnit,"gmcurmin"
JumpTo,BEGIN
NOT_SCOUT:
IsODF,NewUnit,"ivbraz"
IfEQ,false,NOT_RAZOR
GiveWeapon,NewUnit,"gchain_a"
NOT_RAZOR:
IsODF,NewUnit,"ivtank"
IFEQ,false,NOT_TANK
GiveWeapon,NewUnit,"gspstab_c"
NOT_TANK:
IsODF,NewUnit,"ivatank"
IfEQ,false,NOT_ASS_TANK
GiveWeapon,NewUnit,"ghelion_a"
NOT_ASS_TANK:
IsODF,NewUnit,"ivltank"
IfEQ,false,NOT_PUMA
GiveWeapon,NewUnit,"gplasma_c"
NOT_PUMA:
IsODF,NewUnit,"ivmisl"
IfEQ,false,BEGIN
GiveWeapon,NewUnit,"gshadow_c"
JumpTo,BEGIN

[routine,FindPlayer,1,true]
START:
GetPlayer,Player
Wait,1
JumpTo,START