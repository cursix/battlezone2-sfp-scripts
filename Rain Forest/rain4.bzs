// Mission 4 for "The Rain Forest"
// Written by {SFP} Sonic
// Copyrighted The Starfleet Platoon

[objects]
Player
Recycler
Kiln
Cons1
Cons2
Cons3
Enemy_Recycler
Enemy_Factory
Enemy_Armory
Engineer
Patroler
Lab
NewUnit

[positions]

[variables]

[text]
Objective1,
"Protect Lab at all costs!"

Objective2,
"Destroy ISDF Base"

Fury,
"You can now build Fury Tanks"

lost,
"Your Recycler was destroyed!"

[routine,Main,1,true]
GetPlayer,Player
Replace,Player,"fvtank",false
SetPlan,"rain4_temp.aip",5
Display,Objective1,white
Display,Objective2,white
Audio,"rain4a.wav"
Wait,1200
SetPlan,"rain4.aip",5
Wait,1
Replace,Recycler,">fbrecy_1",true
GetByTS,Kiln,1,2
IsAround,Kiln
IfEQ,false,END
Replace,Kiln,">fbforg_1",true
END:
Clear
Display,Objective1,white
Display,Objective2,white
Display,Fury,white
Audio,"rain4b.wav"

[routine,CheckAI,1,true]
GetByTS,Enemy_Recycler,5,1
CHECK:
IsAround,Enemy_Recycler
IfEQ,true,CHECK
GetByTS,Enemy_Factory,5,2
CHECK2:
IsAround,Enemy_Factory
IfEQ,true,CHECK2
Audio,"rain4c.wav"
Clear
Display,Objective2,green
Succeed,10,"win4.txt"

[routine,CheckPlayer,1,true]
GetByTS,Recycler,1,1
GetByLabel,Lab,"over"
BeaconOn,Lab
SetScrap,1,40
CHECK:
IsAround,Recycler
IfEQ,false,RECYL
IsAround,Lab
IfEQ,false,LABL
JumpTo,CHECK
RECYL:
Clear
Display,lost,red
Fail,10,"lose4r.txt"
JumpTo,END
LABL:
Clear
Display,Objective1,red
Fail,10,"lose4l.txt"
END:

[routine,Patroling,1,true]
START:
Createp,Patroler,">ivbraz_r3",5,"patrol1"
Patrol,Patroler,"patrol1",1
Wait,5
Createp,Patroler,"ivbraz_r3",5,"patrol1"
Patrol,Patroler,"patrol1",1
Wait,5
Createp,Patroler,">ivltank_r4",5,"patrol1"
Patrol,Patroler,"patrol1",1
Wait,5
Createp,Patroler,">avhraz_r4",5,"patrol1"
Patrol,Patroler,"patrol1",1
Wait,5
Createp,Patroler,"ivltank_r4",5,"patrol1"
Patrol,Patroler,"patrol1",1
Wait,5
Createp,Patroler,"avhraz_r4",5,"patrol1"
Patrol,Patroler,"patrol1",1
Wait,5
Wait,800
JumpTo,START

[routine,EngineerTime,1,true]
Ally,5,6
Wait,300
SetPlan,"rain4eng.aip",6
START:
Createp,Engineer,">iveng",6,"eng"
CHECK:
IsAround,Engineer
IfEQ,true,CHECK
Wait,300
JumpTo,START

[routine,NewObjectRoutine,1,true]
OnNewObject,100,NewObjectRoutine,NewUnit
BEGIN:
WAITING:
GetByTS,Enemy_Armory,5,3
IsAround,Enemy_Armory
IfEQ,false,WAITING
RunSpeed,NewObjectRoutine,0,true
SetSkill,NewUnit,3
IsODF,NewUnit,"ivscout"
IfEQ,false,NOT_SCOUT
GiveWeapon,NewUnit,"gchain_c"
GiveWeapon,NewUnit,"gmcurmin"
JumpTo,BEGIN
NOT_SCOUT:
IsODF,NewUnit,"ivbraz"
IfEQ,false,NOT_RAZOR
GiveWeapon,NewUnit,"gchain_a"
NOT_RAZOR:
IsODF,NewUnit,"ivtank"
IFEQ,false,NOT_TANK
GiveWeapon,NewUnit,"gspstab_c"
NOT_TANK:
IsODF,NewUnit,"ivatank"
IfEQ,false,NOT_ASS_TANK
GiveWeapon,NewUnit,"gplasma3_a"
NOT_ASS_TANK:
IsODF,NewUnit,"ivltank"
IfEQ,false,NOT_PUMA
GiveWeapon,NewUnit,"gplasma_c"
NOT_PUMA:
IsODF,NewUnit,"ivmisl"
IfEQ,false,BEGIN
GiveWeapon,NewUnit,"gshadow_c"
JumpTo,BEGIN

[routine,FindPlayer,1,true]
START:
GetPlayer,Player
Wait,0
JumpTo,START