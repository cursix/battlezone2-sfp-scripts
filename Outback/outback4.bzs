[objects]
Player
Mole
Recycler
Enemy_Recycler
Enemy_Factory
Defender1
Defender2
Defender3
Sentry1
Sentry2
Sentry3
Spir1
Spir2
Spir3
Spir4
Spir5
Spir6
NewUnit

[positions]

[variables]
TheCompForce,0

[text]
Objective1,
"A large fleet of Scions\nhave gathered to the west.\nKeep and Eye on them\nwhile building your base."

NoAttack,
"Do not attack the Scions"

Objective2,
"Keep an eye out for the\nmole"

Objective3,
"Destroy the Mole"

Objective4,
"Destroy the Scion Base"

Lostrecy,
"Your Recycler was destroyed"

Lostmole,
"The Mole got away"

[routine,Main,1,true]
Wait,1
GetPlayer,Player
Audio,"outback4a.wav"
CamPath,"cam1",5000,5000,Enemy_Recycler
Display,Objective1,white
Display,NoAttack,white
Wait,300
Createp,Defender1,">fvtankout2",5,"defenders"
Createp,Defender2,"fvtankout2",5,"defenders"
Createp,Defender3,"fvtankout2",5,"defenders"
Createp,Sentry1,">fvsentout1",5,"defenders"
Defend,Sentry1,Defender1,1
Createp,Sentry2,"fvsentout1",5,"defenders"
Defend,Sentry2,Defender2,1
Createp,Sentry3,"fvsentout1",5,"defenders"
Defend,Sentry3,Defender3,1
Audio,"outback4b.wav"
Wait,4
Display,Objective2,white
Wait,300
Createp,Mole,">ivtankout1",5,"mole"
SetPTeam,Mole,1
BeaconOn,Mole
GoTo,Mole,"mole_run",5
Audio,"outback4c.wav"
Defend,Defender1,Mole,5
Defend,Defender2,Mole,5
Defend,Defender3,Mole,5
Clear
Display,Objective3,white
CHECK_MOLE:
IsAround,Mole
IfEQ,false,DESTROYED
DistObject,Mole,Enemy_Recycler
IfGT,200,CHECK_MOLE
Clear
Display,Lostmole,red
Fail,5,"outback4lm.txt"
JumpTo,END
DESTROYED:
Audio,"outback4d.wav"
Clear
Display,Objective3,green
Wait,4
Display,Objective4,white
CHECK_RECYCLER:
IsAround,Enemy_Recycler
IfEQ,true,CHECK_RECYCLER
GetByTS,Enemy_Factory,5,2
CHECK_ENEMYFACT:
IsAround,Enemy_Factory
IfEQ,true,CHECK_ENEMYFACT
Clear
Display,Objective4,green
Audio,"outback4e.wav"
Succeed,10,"outback4w.txt"
END:

[routine,Bases,1,true]
GetByLabel,Enemy_Recycler,"enemy_recy"
GetByLabel,Recycler,"recy"
SetScrap,1,40
SetScrap,5,40
SetPlan,"outback4.aip",5
Createp,Spir1,"fbspir",5,"spir1"
Createp,Spir2,"fbspir",5,"spir2"
Createp,Spir3,"fbspir",5,"spir3"
Createp,Spir4,"fbspir",5,"spir4"
Createp,Spir5,"fbspir",5,"spir5"
Createp,Spir6,"fbspir",5,"spir6"
RunSpeed,Bases,0,true
CHECK:
IsAround,Recycler
IfEQ,true,CHECK
Clear
Display,Lostrecy,red
Fail,5,"outbacklr.txt"

[routine,NewObjectRoutine,1,true]
OnNewObject,100,NewObjectRoutine,NewUnit
GetCompForce,TheCompForce
Wait,60
START:
RunSpeed,NewObjectRoutine,0,true
Set,TheCompForce,3
Add,TheCompForce,0
IfEQ,1,SMALL_FORCE
IfEQ,2,MED_FORCE
IfEQ,3,LARGE_FORCE
SMALL_FORCE:
SetSkill,NewUnit,2
JumpTo,BEGIN
MED_FORCE:
SetSkill,NewUnit,3
JumpTo,BEGIN
LARGE_FORCE:
SetSkill,NewUnit,4
JumpTo,BEGIN
BEGIN:
IsODF,NewUnit,"fvscout"
IfEQ,false,NOT_SCOUT
GiveWeapon,NewUnit,"gquill_c"
GiveWeapon,NewUnit,"gshield"
JumpTo,START
NOT_SCOUT:
IsODF,NewUnit,"fvtank"
IfEQ,false,NOT_TANK
GiveWeapon,NewUnit,"garc_c"
GiveWeapon,NewUnit,"gshield"
JumpTo,START
NOT_TANK:
IsODF,NewUnit,"fvsent"
IfEQ,false,NOT_SENTRY
GiveWeapon,NewUnit,"ggaus"
GiveWeapon,NewUnit,"gshield"
NOT_SENTRY:
IsODF,NewUnit,"fvarch"
IfEQ,false,START
GiveWeapon,NewUnit,"gabsorb"
JumpTo,START

[routine,FindPlayer,1,true]
START:
Wait,2
GetPlayer,Player
JumpTo,START